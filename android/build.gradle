group 'how.virc.flutter_esp_ble_prov'
version '1.0-SNAPSHOT'

import java.util.Properties

buildscript {
    ext.kotlin_version = '1.9.24'
    repositories {
        google()
        maven { url ("https://jitpack.io/") }
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterSdkPath = localProperties.getProperty('flutter.sdk')
if (flutterSdkPath == null || flutterSdkPath.trim().isEmpty()) {
    flutterSdkPath = System.getenv('FLUTTER_ROOT') ?: System.getenv('FLUTTER_HOME')
}
if (flutterSdkPath == null || flutterSdkPath.trim().isEmpty()) {
    try {
        def process = ['cmd', '/c', 'where flutter'].execute()
        process.waitFor()
        if (process.exitValue() == 0) {
            def first = process.in.text.readLines().find { it?.trim() }
            if (first != null) {
                def flutterBat = new File(first.trim())
                flutterSdkPath = flutterBat.parentFile.parentFile.absolutePath
            }
        }
    } catch (Exception ignored) {
    }
}
def flutterJar = flutterSdkPath == null
        ? null
        : new File(flutterSdkPath, 'bin/cache/artifacts/engine/android-x64/flutter.jar')

android {
    namespace "how.virc.flutter_esp_ble_prov"

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 23
    }
}

dependencies {
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.google.crypto.tink:tink-android:1.6.1'
    implementation 'com.google.protobuf:protobuf-javalite:3.18.0'
    implementation 'com.github.espressif:esp-idf-provisioning-android:lib-2.1.2'

    if (flutterJar != null && flutterJar.exists()) {
        compileOnly files(flutterJar.absolutePath)
    } else {
        logger.warn('Flutter embedding jar was not found. Standalone Gradle compile may fail. Resolved flutterSdkPath=' + flutterSdkPath)
    }
}
